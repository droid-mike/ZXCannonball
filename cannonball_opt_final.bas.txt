100 REM === BASIC Month 3, Cannonball
110 REM === http://reddit.com/r/RetroBattlestations
120 REM === written by FozzTexx
130 REM === ZX Spectrum ASCII version by mattinx
135 REM ===
140 REM === ZX-81/TS1000 version by /u/droid_mike
150 REM ===
160 REM === Significantly optimized due to the slow speed of the ZX81
170 REM === Canon angles and flight paths precalculated in fast mode
180 REM ===
200 REM === Clear the screen and setup variables
210 CLS
220 LET COLS=32
222 LET ROWS=21
224 REM Columns and Rows of screen
230 LET CA=45
232 LET CL=3
234 REM Angle and length of cannon
240 LET CX=5
244 LET SC=0
246 LET LV=3
260 LET LA=15
262 LET PXI=0
264 LET BXI=0
266 LET BYI=0
268 LET BANX=1
269 LET TC=0
270 REM SETUP PLANE:
275 GOSUB 6000
276 REM PRECALC CANON ANGLES AND PUT INTO ARRAY:
280 GOSUB 7000
300 REM === Play game
305 CLS
306 FAST
307 LET BANX=INT(RND*COLS)+1
309 LET PX=COLS-(4+INT(RND*10))
310 LET CV=INT (RND*15)+10
311 GOSUB 4000
312 REM ***Show Lives/Velocity
313 GOSUB 5000
314 GOSUB 2000
315 GOSUB 1000
316 REM ***Draw Player
317 SLOW
320 GOSUB 6500
322 REM ***Read the keyboard
325 GOSUB 2500
328 LET CAO=CA
330 IF K$="A" OR K$="7" THEN LET CA=CA+5
332 IF CA>85 THEN LET CA=85
340 IF K$="Z" OR K$="6" THEN LET CA=CA-5
342 IF CA<15 THEN LET CA=15
344 IF K$="8" THEN LET CV=CV+1
346 IF K$="5" THEN LET CV=CV-1
347 IF K$="5" OR K$="8" THEN GOTO 392
350 IF K$="Q" THEN STOP
360 IF K$="M" THEN GOTO 400
370 IF CAO<>CA THEN GOTO 396
390 GOTO 320
392 GOSUB 4000
394 GOTO 325
396 GOSUB 1000
398 GOTO 325

400 REM === Fire
405 LET BANX=COLS*2
406 GOSUB 6500
407 REM This clears the plane from the screen
410 LET LX=(CL+1)*COS ((360-CA)*PI/180)
412 LET LY=(CL+1)*SIN ((360-CA)*PI/180)
420 LET BX=CX+LX
422 LET BY=ROWS+LY
430 GOSUB 1500
440 LET VX=CV*COS ((360-CA)*PI/180)
442 LET VY=CV*SIN ((360-CA)*PI/180)
500 REM === Move player and animate ball
510 REM GOSUB 6500
515 GOSUB 2500
520 IF K$="5" THEN LET PX=PX-1
522 IF PX<CX+CL THEN LET PX=CX+CL
530 IF K$="8" THEN LET PX=PX+1
532 IF PX>COLS-1 THEN LET PX=COLS-1
540 IF K$="5" OR K$="8" THEN GOSUB 2000
550 LET BX=BX+(VX/10)
552 LET BY=BY+(VY/10)
554 GOSUB 1500
560 LET VY=VY+2
570 IF BY<ROWS THEN GO TO 500
580 LET P$=" "
582 LET XDRAW=BXI
584 LET YDRAW=BYI
586 GOSUB 3500
600 REM === Score or die
610 LET LX=BX-BXI
612 LET LY=BY-BYI
614 LET L2=ROWS-BYI
616 LET LA=0
620 LET IX=BXI+LX*(L2/LY)
630 IF NOT(IX>=PX-1 AND IX<=PX+1) THEN GOTO 640  
632 LET SC=SC+10
634 GO TO 300
636 REM ENDIF
640 LET LV=LV-1
645 LET XDRAW=IX-2
646 LET YDRAW=ROWS-1
647 LET P$="SPLAT"
648 GOSUB 3500
650 GOSUB 4000
651 IF LV>=1 THEN GOTO 660
652 LET P$="GAME OVER"
654 LET XDRAW=INT ((COLS-LEN P$)/2)
656 LET Y1I=INT (ROWS/2)
657 GOSUB 3500
658 STOP
660 GO TO 300

1000 REM === Draw Cannon
1005 REM FAST
1010 LET P$="Angle: "+STR$ CA+" "
1012 LET XDRAW=2
1014 LET YDRAW=2
1016 GOSUB 3500
1100 REM NEW CODE
1105 LET XDRAW=CX
1110 LET YDRAW=ROWS-2
1120 LET P$=C$((CA-10)/5, 1 TO 2)
1130 GOSUB 3500
1140 LET YDRAW=ROWS-1
1150 LET P$=C$((CA-10)/5, 3 TO 4)
1160 GOSUB 3500
1170 LET YDRAW=ROWS
1175 LET XDRAW=CX-1
1180 LET P$=CHR$(128)+CHR$(128)+CHR$(128)
1190 GOSUB 3500
1200 RETURN

1500 REM === Draw ball
1510 LET XDRAW=INT BX
1512 LET YDRAW=INT BY
1514 LET P$=CHR$(136)
1516 GOSUB 3500
1520 IF NOT(BXI<>XDRAW OR BYI<>YDRAW) THEN GOTO 1530
1522 LET XDRAW=BXI
1524 LET YDRAW=BYI
1526 LET P$="."
1528 GOSUB 3500
1530 LET BXI=INT BX
1532 LET BYI=INT BY
1540 RETURN

2000 REM === Draw Player
2010 LET P$=" "+CHR$(133)+CHR$(131)+CHR$(5)+" "
2012 LET XDRAW=INT PX-2
2014 LET YDRAW=INT ROWS
2016 GOSUB 3500
2020 REM IF PXI<>XDRAW THEN LET XDRAW=PXI-1: LET P$="    "
2022 REM GOSUB 3500
2030 REM LET PXI=INT PX
2040 RETURN

2500 REM === Read the keyboad
2510 LET K$=INKEY$
2520 RETURN

3000 REM === Plot a line
3010 LET XDRAW=INT X1
3012 LET X2I=INT X2
3014 LET YDRAW=INT Y1
3016 LET Y2I=INT Y2
3020 LET DXI=ABS (X2I-XDRAW)
3022 LET SXI=-1
3024 IF XDRAW<X2I THEN LET SXI=1
3030 LET DYI=ABS (Y2I-YDRAW)
3032 LET SYI=-1
3034 IF YDRAW<Y2I THEN LET SYI=1
3040 LET ERI=-DYI
3042 IF DXI>DYI THEN LET ERI=DXI
3050 LET ERI=ERI/2
3060 GOSUB 3500
3070 IF XDRAW=X2I AND YDRAW=Y2I THEN RETURN
3080 LET E2I=ERI
3090 IF E2I<=-DXI THEN GOTO 3100
3092 LET ERI=ERI-DYI
3094 LET XDRAW=XDRAW+SXI
3100 IF XDRAW=X2I AND YDRAW=Y2I THEN RETURN
3110 IF E2I>=DYI THEN GOTO 3120
3112 LET ERI=ERI+DXI
3114 LET YDRAW=YDRAW+SYI
3120 IF XDRAW=X2I AND YDRAW=Y2I THEN RETURN
3130 GO TO 3060

3500 REM === Plot a point
3510 IF XDRAW<1 OR XDRAW>COLS OR YDRAW<1 OR YDRAW>ROWS THEN RETURN
3520 PRINT AT YDRAW-1,XDRAW-1;P$
3540 RETURN

4000 REM === Show score/lives/velocity
4010 LET P$="Velocity: "+STR$ CV+" "
4012 LET XDRAW=2
4014 LET YDRAW=1
4016 GOSUB 3500
4020 LET P$="Score: "+STR$ SC
4022 LET XDRAW=INT (COLS/2)
4024 GOSUB 3500
4025 LET LVE=LV+3
4027 IF LVE>6 THEN LET LVE=6
4030 LET P$="   UUU"(LV+1 TO LVE)
4032 LET XDRAW=COLS-3
4034 GOSUB 3500
4040 RETURN

5000 REM Draw Ground
5010 FOR I=0 TO COLS-1
5020 PRINT AT ROWS,I; CHR$(8)
5030 NEXT I
5040 RETURN

6000 REM SETUP PLANE
6005 FAST
6010 DIM B$(3,COLS*3)
6020 LET B$(1,COLS TO COLS+5)=" "+CHR$(9)+" "+CHR$(129)+" "
6030 LET B$(2,COLS TO COLS+5)=CHR$(6)+CHR$(3)+CHR$(128)+CHR$(3)+"<"
6040 LET B$(3,COLS TO COLS+5)="   "+CHR$(134)
6050 FOR I=COLS+6 TO COLS*2-1
6060 LET B$(1,I TO I)=CHR$(9)
6070 LET B$(2,I-1 TO I-1)=CHR$(8)
6080 LET B$(3,I TO I)=CHR$(10)
6090 NEXT I
6092 FOR I=1 TO 3
6093 PRINT B$(I)
6094 NEXT I
6095 RETURN

6500 REM FLY PLANE
6510 PRINT AT 4,0;B$(1,BANX TO BANX+COLS-1)
6520 PRINT AT 5,0;B$(2,BANX TO BANX+COLS-1)
6530 PRINT AT 6,0;B$(3,BANX TO BANX+COLS-1)
6540 LET BANX=BANX+1
6545 IF BANX<COLS/2 OR BANX>COLS+COLS/2 THEN LET BANX=BANX+1
6550 IF BANX>COLS*2 THEN LET BANX=1
6610 IF TC=0 THEN LET B$(2,COLS+7 TO COLS+29)="/R/RETROBATTLESTATIONS "
6620 IF TC=1 THEN LET B$(2,COLS+7 TO COLS+29)=" CANNONBALL FOR ZX-81  "
6630 IF TC=2 OR TC=3 THEN LET B$(2,COLS+7 TO COLS+29)="7/21/18 BY U/DROID"+CHR$(131)+"MIKE"
6640 IF TC=4 THEN LET B$(2,COLS+7 TO COLS+29)="    ALL HAIL FOZZTEXX  "
6645 IF TC=5 OR TC=6 THEN LET B$(2,COLS+7 TO COLS+29)="KEYS: ARROWS=AIM M=FIRE"
6650 LET TC=TC+1
6660 IF TC>6 THEN LET TC=0
6670 RETURN

7000 REM INITIALIZE GRAPHICS FOR CANNON ANGLES
7010 DIM C$(15,4)
7020 LET C$(1)="  "+CHR$(131)+CHR$(131)
7025 LET C$(2)="  "+CHR$(131)+CHR$(131)
7030 LET C$(3)="  "+CHR$(131)+CHR$(129)
7040 LET C$(4)="  "+CHR$(131)+CHR$(6)
7050 LET C$(5)=" "+CHR$(131)+CHR$(6)
7060 LET C$(6)=" "+CHR$(129)+CHR$(6)
7070 LET C$(7)=" "+CHR$(6)+CHR$(6)
7080 LET C$(8)=" "+CHR$(6)+CHR$(133)
7090 LET C$(9)=CHR$(6)+" "+CHR$(6)
7095 LET C$(10)=CHR$(6)+" "+CHR$(129)
7100 LET C$(11)=CHR$(6)+" "+CHR$(130)
7110 LET C$(12)=CHR$(133)+" "+CHR$(130)
7120 LET C$(13)=CHR$(5)+" "+CHR$(130)
7130 LET C$(14)=CHR$(5)+" "+CHR$(128)
7135 LET C$(15)=CHR$(5)+" "+CHR$(5)
7140 RETURN





